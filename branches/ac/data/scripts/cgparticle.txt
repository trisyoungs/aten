# Visualisation of coarse-grained particles (Example 3 in published paper)

# Create benzene model
int n;
newmodel("benzene");
for (n=1; n<7; ++n)
{
	move(0, 1.39, 0);
	newatom(C);
	rotz(-60.0);
	move(0, 1.0, 0);
	newatom(H);
	move(0, -1.0, 0);
	rotz(120);
}
rebond();
selectall();
copy();
setnmols(200);

# Create fluorobenzene model
newmodel("fluorobenzene");
paste();
select(H);
transmute(F);
setnmols(200);

# Create naphthalene model
newmodel("naphthalene");
for (n=1; n<11; ++n)
{
	chain(C, n%2+1);
	if (n%5 == 0) rotz(-60.0);
	else rotz(60.0);
	move(0, 1.39, 0);
}
newbond(1, 10, "double");
newbond(5, 10);
addhydrogen();
setnmols(100);

# Create mixture model
newmodel("mixture");
cell(100, 60, 25, 90, 90, 90);
loadff("uff.ff");
vdwscale(0.6);
disorder(4);
mcminimise(5);

# Fold molecules into cell and add ellipsoids
foldmolecules();
selectpattern(1);
aten.prefs.glyphcolour = {1.0, 1.0, 1.0, 1.0};
autoellipsoids();
selectpattern(2);
aten.prefs.glyphcolour = {0.2, 1.0, 0.2, 1.0};
autoellipsoids();
selectpattern(3);
aten.prefs.glyphcolour = {0.0, 0.0, 1.0, 1.0};
autoellipsoids();
# Hide original atoms
selectall();
hide();
