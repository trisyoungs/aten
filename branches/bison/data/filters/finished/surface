# Simple 2D height map data definition (for v1.1+)
# Created:	 15/04/2009
# Last modified: 15/04/2009
# ChangeLog:
# Notes:

filter(type="importgrid", name="2D Surface Data", nickname="surf", extension="surf", glob="*.surf")
{
	# Variable declarations
	int n,nx,ny,nz,npoints;
	vector origin, vx, vy;
	double data;
	string order;

	# Create new grid
	newgrid(filterfilename());

	# First line contains number of gridpoints in each direction x,y
	readline(nx,ny);
	gridsize(nx,ny,0);
	# Second line contains vectors between gridpoints
	readline(vx.x, vx.y, vx.z, vy.x, vy.y, vy.z);
	gridaxes(vx.x, vx.y, vx.z, vy.x, vy.y, vy.z, 0, 0, 1);
	# Third line contains grid origin (lower left-hand corner)
	readline(origin.x, origin.y, origin.z);
	gridorigin(origin.x, origin.y, origin.z);
	# Fourth line contains loop order
	readline(order);
	gridlooporder(order);

	# Read in grid data
	npoints = nx*ny;
	for (n=0; n<npoints; ++n)
	{
		readline(data);
		addnextgridpoint(data);
	}
	finalisegrid();
}
