# GAMESS-US Trajectory Filter, and Cartesian Input Model Filters (I/O)
# Last modified: 06/01/2009
# Notes:
#	Output model filter sets symmetry to C1.
# ChangeLog:
#	06/01/2009 - Renamed 'nmodels' variable to prevent clash with implicit variable of same name
#	10/10/2008 - Adjusted to use new data access methods.
#	04/10/2008 - Format delimiter reverted from '%' to '@'.
#	05/08/2008 - Changed extension from 'out' to 'log'. Added reading of .inpi files. Nicknames now 'gamuslog' and 'gamusinp'

importmodel
	name "GAMESS-US Log Files"
	extension log
	glob *.log
	nickname gamuslog
	zmap name

	# Variable declaration
	integer result,nstructures,natoms,n
	character line,e
	real rx,ry,rz

	# Determine number of atoms - search for line containing 'TOTAL NUMBER OF ATOMS'
	find "TOTAL NUMBER OF ATOMS" $result $line
	if $result = 0
		error "Couldn't determine number of atoms from GAMESS-US output."
	end
	# Get number of atoms from line
	readvar $line "*@48 $natoms"
	# Now search for coordinate sets
	let $nstructures = 0
	for $n
		# Determine number of atoms - search for lines containing 'COORDINATES OF ALL ATOMS ARE'
		find "COORDINATES OF ALL ATOMS ARE" $result
		if $result = 1
			# Found a set of coordinates. Skip 2 lines and then read coordinates
			$nstructures += 1
			skipline 2
			newmodel $nstructures
			for $n,1,$natoms
				readline "$e * $rx $ry $rz"
				newatom $e
				setcoords $rx $ry $rz
			end
			# Recalculate bonding
			rebond
			finalisemodel
		end
	end
end

importmodel
	name "GAMESS-US Cartesian Input"
	extension inp
	glob *.inp
	nickname gamusinp
	id 5
	zmap name

	# Variables
	character symm,tester,symbol,title
	integer n,z
	real rx,ry,rz

	# Search for $DATA section
	find "DATA" $n
	if $n = 0
		error "Couldn't find 'DATA' section in input file."
	end

	# Read in title card
	getline $title
	newmodel $title

	# Skip over symmetry specification
	readline "$symm"
	if $symm <> C1
		# Single blank line means the master frame is in use, and only this line to skip
		readline "$tester"
		if $tester <> ""
			skipline
		end
	end
	
	# Coordinates follow, terminated by $END
	for $n
		readline "$symbol $z $rx $ry $rz
		if $symbol = "$END"
			break
		end
		newatom $symbol $rx $ry $rz
	end

	# Done
	rebond
	finalisemodel
end

exportmodel
	name "GAMESS-US Cartesian Input"
	extension inp
	glob *.inp
	nickname gamusinp
	id 5

	# Variable declarations
	atom i

	# Write a standard control header
	writeline " \$SYSTEM MEMDDI=16 \$END"
	writeline " \$CONTRL SCFTYP=RHF RUNTYP=OPTIMIZE COORD=CART ICHARG=0 MULT=1 \$END"
	writeline " \$STATPT NSTEP=100 \$END"
	writeline " \$SYSTEM TIMLIM=1200 MWORDS=96 \$END"
	writeline " \$BASIS GBASIS=N31 NGAUSS=6 NDFUNC=1 \$END"
	writeline " \$GUESS GUESS=HUCKEL \$END"
	writeline " \$SCF DIRSCF=.TRUE. \$END"

	# Now for the DATA section
	writeline " \$DATA"
	writeline "$model.name"
	writeline "C1"
	for $i
		writeline "$i.name@10  $i.z@4 $i.rx@12.6 $i.ry@12.6 $i.rz@12.6"
	end
	writeline " \$END"
end
