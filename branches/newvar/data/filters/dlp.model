# DL_POLY Configuration Import / Export
# Last modified: 05/08/08

importmodel
	name "DL_POLY Configuration"
	extension CONFIG,REVCON,config,revcon
	glob *CON*
	exact CONFIG,REVCON,config,revcon
	nickname dlpoly
	id 1
	zmap ff

	# Variable Declaration
	character e, title
	integer n,keytrj,imcon
	double ax,ay,az,bx,by,bz,cx,cy,cz
	double rx,ry,rz,vx,vy,vz,fx,fy,fz

	# Title section
	getline $title
	newmodel $title
	readline "$keytrj $imcon"

	# Cell (if present)
	if $imcon <> 0
		readline "$ax $ay $az"
		readline "$bx $by $bz"
		readline "$cx $cy $cz"
		cellaxes $ax $ay $az $bx $by $bz $cx $cy $cz
	end

	# Atoms
	for $n
		readline "$e"
		newatom $e
		readline "$rx $ry $rz"
		setcoords $rx $ry $rz
		if $keytrj > 0
			readline "$vx $vy $vz"
			setvelocities $vx $vy $vz
		end
		if $keytrj > 1
			readline "$fx $fy $fz"
			setforces $fx $fy $fz
		end
	end
	rebond
	fold
	finalisemodel
end

exportmodel
	name "DL_POLY Configuration"
	extension CONFIG
	glob *CON*
	nickname dlpoly
	id 1

	# Variable Declaration
	integer imcon,id,n
	double rx, ry, rz
	atom i

	# Title section
	writeline "$model.name"
	# Convert celltype string to DL_POLY-style integer
	if $model.cell.type = "none"
		$imcon = 0
	elseif $model.cell.type = "cubic"
		$imcon = 1
	elseif $model.cell.type = "orthorhombic"
		$imcon = 2
	elseif $model.cell.type = "parallelepiped"
		$imcon = 3
	end
	writeline "         0$imcon%010"

	# Cell (if present) 
	if $imcon <> 0
		writeline "$model.cell.ax%20.12$model.cell.ay%20.12$model.cell.az%20.12"
		writeline "$model.cell.bx%20.12$model.cell.by%20.12$model.cell.bz%20.12"
		writeline "$model.cell.cx%20.12$model.cell.cy%20.12$model.cell.cz%20.12"
	end

	# Atoms
	for $n,1,$model.natoms
		# Get variable pointer to make manipulation a little less cluttered
		$i = $model.atoms[$i]
		$rx = $i.rx-$model.cell.centrex
		$ry = $i.ry-$model.cell.centrey
		$rz = $i.rz-$model.cell.centrez
		writeline "${i.type%-8}${i.id%10}   $i.mass"
		writeline "$rx%20.14$ry%20.14$rz%20.14"
	end
end
