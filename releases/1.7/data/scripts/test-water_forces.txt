# Load reference forces models - Note that all forces in these files are in units of 10J/mol rather than kJ/mol
loadmodel("data/test/water-forces-elec.CONFIG");
model elecref = aten.model;
loadmodel("data/test/water-forces-vdw.CONFIG");
model vdwref = aten.model;
loadmodel("data/test/water-forces-intra.CONFIG");
model intraref = aten.model;

# Create test forcefield (compatible with reference forces)
forcefield waterff = newff("Water Test");
waterff.units = "kj";

# Create type definitions
waterff.addtype(1,"HW","HW",H, "nbonds=1,-O(nh=2)", "Water hydrogen");
waterff.addtype(2,"OW","OW",O, "nbonds=2,nh=2", "Water oxygen");
waterff.addinter("lj", 1, 0.4238, 0.0, 0.0);
waterff.addinter("lj", 2, -0.8476, 0.65, 3.166);
waterff.addbond("harmonic", "HW", "OW", 4184.0, 1.0);
waterff.addangle("harmonic", "HW", "OW", "HW", 414.0, 109.5);
waterff.finalise();

# Load another copy of one of the reference models so we have the coordinates
loadmodel("data/test/water-forces-vdw.CONFIG");

# Check various force components
aten.prefs.eleccutoff = 10.0;
aten.prefs.vdwcutoff = 10.0;
aten.prefs.elecmethod = "ewald";
aten.prefs.ewaldalpha = 0.36292;
aten.prefs.ewaldkmax = {8,8,8}; 
vector v;

# Electrostatics (via Ewald sum)
aten.prefs.calculateintra = FALSE;
aten.prefs.calculatevdw = FALSE;
modelforces();
double rmse_elec = 0.0;
for (int i=1; i<=aten.model.natoms; ++i)
{
	v = aten.model.atoms[i].f - elecref.atoms[i].f/100.0;
	rmse_elec += v.x*v.x + v.y*v.y + v.z*v.z;
}
rmse_elec = sqrt(rmse_elec / aten.model.natoms);

# Intramolecular terms
aten.prefs.elecmethod = "none";
aten.prefs.calculateintra = TRUE;
aten.prefs.calculatevdw = FALSE;
modelforces();
double rmse_intra = 0.0;
for (int i=1; i<=aten.model.natoms; ++i)
{
	v = aten.model.atoms[i].f - intraref.atoms[i].f/100.0;
	rmse_intra += v.x*v.x + v.y*v.y + v.z*v.z;
}
rmse_intra = sqrt(rmse_intra / aten.model.natoms);

# Short-range (vdW)
aten.prefs.elecmethod = "none";
aten.prefs.calculateintra = FALSE;
aten.prefs.calculatevdw = TRUE;
modelforces();
double rmse_vdw = 0.0;
for (int i=1; i<=aten.model.natoms; ++i)
{
	printf(" D/A = %f %f %f  %f %f %f\n", aten.model.atoms[i].fx,aten.model.atoms[i].fy,aten.model.atoms[i].fz,vdwref.atoms[i].fx/100.0,vdwref.atoms[i].fy/100.0,vdwref.atoms[i].fz/100.0);
	v = aten.model.atoms[i].f - vdwref.atoms[i].f/100.0;
	rmse_vdw += v.x*v.x + v.y*v.y + v.z*v.z;
}
rmse_vdw = sqrt(rmse_vdw / aten.model.natoms);

printf("Electrostatic  force RMSE = %f\n", rmse_elec);
printf("Short-range    force RMSE = %f\n", rmse_vdw);
printf("Intramolecular force RMSE = %f\n", rmse_intra);

quit();

