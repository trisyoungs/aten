# Script to build an amorphous silica system

int nsio2 = option("SiO2 Units", "intspin", 1,1000000,200,10,"group=System");
double density = option("Density", "doublespin", 0.1,4.0,2.0,0.1, "group=System");

# Determine system size, assuming cubic cell
double mass = 32.06 + 2*15.9994;
printf("Silica mass = %f\n", mass);
double volume = nsio2 * (mass / density) * 1.0E24 / AVOGADRO;
printf("Volume of cell = %f\n", volume);
double l = volume^(1.0/3.0);
printf("Length of cubic cell = %f\n", l);

# Create temporary forcefield
forcefield sio2ff = newff("SiO2");
sio2ff.units = "kj";
sio2ff.addtype(1,"Si","Si",Si, "");
sio2ff.addtype(2,"O","O",O, "");
sio2ff.addinter("lj", 1, 0.0, 0.1, 2.0);
sio2ff.addinter("lj", 2, 0.0, 0.1, 2.0);
sio2ff.finalise();

# Create new models and associate ff
model msi = newmodel("Si");
newatom(Si);
setnmols(nsio2);
ffmodel();
model mo = newmodel("O");
newatom(O);
setnmols(2*nsio2);
ffmodel();
newmodel("Amorphous Silica");
cell(l,l,l,90,90,90);
ffmodel();

disorder(1000);

# Clean-up
deletemodel(msi);
deletemodel(mo);
deleteff(sio2ff);
