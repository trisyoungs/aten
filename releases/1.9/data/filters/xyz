# XMol-style XYZ coordinates files (for v1.2+)
# Created:	 25/03/2009
# Last modified: 02/09/2011
# ChangeLog:
#	02/09/2011 - Tweaked to use capitalised command/member names in v1.8

filter(type="importmodel",name="XMol XYZ Coordinates", nickname="xyz", extension="xyz", glob="*.xyz", id=3)
{
	# Variable declaration
	int natoms,n,m;
	string e,title;
	double rx,ry,rz,q;
	atom i;

	# Read data
	while (!eof())
	{
		readLine(natoms);
		getLine(title);
		newModel(title);
		for (n=1; n<=natoms; ++n)
		{
			readLine(e,rx,ry,rz,q);
			i = newAtom(e, rx, ry, rz);
			i.q = q;
		}
		rebond();
		finaliseModel();
	}
}

filter(type="exportmodel",name="XMol XYZ Coordinates", extension="xyz", glob="*.xyz", nickname="xyz", id=3)
{
	# Variable declaration
	model m = aten.frame;

	writeLine(m.nAtoms);
	writeLine(m.name);
	for (atom i=m.atoms; i != 0; ++i) writeLineF("%-8s  %12.6f %12.6f %12.6f %12.6f\n",i.symbol,i.rx,i.ry,i.rz,i.q);
}

filter(type="importtrajectory",name="XMol XYZ Coordinates", nickname="xyz", extension="xyz", glob="*.xyz", id=3)
{
	int readHeader()
	{
		return TRUE;
	}

	int readFrame()
	{

		# Variable declaration
		int natoms,n;
		string e,title;
		double rx,ry,rz,q;
		atom i;

		# Read frame
		if (readLine(natoms) == 0) return FALSE;
		getLine(title);
		for (n=1; n<=natoms; ++n)
		{
			readLine(e,rx,ry,rz,q);
			i = newAtom(e, rx, ry, rz);
			i.q = q;
		}
		rebond();
		return TRUE;
	}

}

