#
# Script to create disordered [dmim]Cl system
#

int n = 10;

# Load Canongia Lopes ionic liquids forcefield (giving it a nickname of 'ilff')
# This will become the current forcefield (used by the 'ff' commands later on)
loadff("data/ff/cldp-il.ff", "ilff");

# Prepare first component to add in - [dmim] cation
# Load model from file and call it 'dmim'
double mass;
loadmodel("data/test/siesta-dmim.xyz","dmim");
mass = aten.model.mass;
setnmols(10);

# Associate the current forcefield (the only one) to the model
ffmodel();

# Create a forcefield expression for the model (determine atom types, bond types, etc.)
createexpression();

# Assign atomic partial charges from the forcefield as well
chargeff();

# Print out the model data 
info();

# Prepare second component - chloride anion
# Create a new model (naming it 'chloride') and draw a chloride atom
newmodel("chloride");
newatom(Cl);
setnmols(n);
mass += aten.model.mass;

# Associate forcefield to the model
ffmodel();

# Create the forcefield expression
createexpression();

# Assign charges from the forcefield
chargeff();

# Print the data
info();

# New we're ready to use these two components to create a new system.
# First, create another empty model (called 'box') and create a cubic
# cell in it
newmodel("box");
double density = 1.1016;  # g/cm3 at 450 K
double mvol = (mass / density) * 1.0E24 / 6.02213E23;
double l = (n * mvol)^(1.0/3.0);
cell(l,l,l,90,90,90);

# Associate the forcefield with the box (there are no atoms in it, but it still needs a valid forcefield)
ffmodel();

# Request that we want 100 molecules maximum of each (how many you end up with will vary)

# Tweak the Monte Carlo parameters for the disordered insertion
mcntrials("insert",100);
mcaccept("insert",500.0);
printmc();

# Request the Ewald sum for electrostatics (or none for just VDW intermolecular energies)
#elec ewald 0.256 10 10 10
#elec none

# Print out the current component list for the disordered builder
listcomponents();

# To squeeze more molecule in, we can adjust the VDW radii of all atoms with this scale factor
vdwscale(0.75);

# Begin! Run the Monte Carlo for 10 cycles
disorder(100);

# Save out the current model (will be 'box', since it was the last created) as a DL_POLY config file
savemodel("dlpoly", "dmimcl.CONFIG");
savemodel("mopac", "dmimcl.mop");

# Done.
quit();
