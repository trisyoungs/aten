# Generate water droplet in methane (Example 1 in published paper)

# Create water model, and assign it to partition 2 (the sphere)
newmodel("water");
newatom(O);
addhydrogen();
setupcomponent("density", 2, 0, 0.97);

# Create methane model
newmodel("methane");
newatom(C);
addhydrogen();
setupcomponent("density", 1, 0, 0.656);

# Create mixed system
newmodel("droplet");
cell(20, 20, 20, 100, 100, 100);
disorder("Sphere,r=0.3");

# Add glyphs to methane molecules
pattern p;
int i,m;
p = getpattern("methane");
i = p.firstatomid;
for (m=1; m<=p.nmols; ++m)
{
	newglyph("tetrahedron");
	glyphatomsr(i+1, i+2, i+3, i+4);
	i += p.nmolatoms;
}
foldmolecules();

# Load UFF so we can save an expression
loadff("uff.ff");
ffmodel();
savemodel("dlpoly", "gaswater.CONFIG");
saveexpression("dlpoly", "gaswater.FIELD");
