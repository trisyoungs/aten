# Protein databank coordinates import. Residue data etc. is ignored.
# Last modified: 14/12/2008
# ChangeLog:
#	14/12/2008 - Fixed reporting of missing HEADER, even when HEADER was supplied.
#	16/10/2008 - Corrected dummy variable formatter which contained a rogue '%'.
#	12/10/2008 - Adjusted to use new variable access methods (but kept old keyword style).
#	04/10/2008 - Format delimiter reverted from '%' to '@'.
#	05/08/2008 - Quoting adjusted to follow new Parser rules in r550.

importmodel
	name "Protein Databank PDB"
	nickname pdb
	extension pdb
	glob *.pdb

	# Variable declarations
	character keyword,line,e,name
	real rx,ry,rz,ca,cb,cc,alpha,beta,gamma
	integer n,id,i,bondi,bondj,bondk,bondl,found

	# Element names in PDB files are commonly 'appended' by a numeric id, which we don't want...
	zmap firstalpha

	# Create default model
	newmodel $infile
	$found = 0
	for $n
		readline "$keyword@6 $line@100"
		if $keyword = "HEADER"
			let $found = 1
			setname $line
		elseif $keyword = "CRYST1"
			readvar $line "$ca@9 $cb@9 $cc@9 $alpha@7 $beta@7 $gamma@7"
			cell $ca $cb $cc $alpha $beta $gamma"
		elseif $keyword = "HETATM"
			#readvar $line "*@5 $e@4 *@1 *@1 *@3 *@1 *@1 *@4 *@1 *@3 $rx@8 $ry@8 $rz@8 *@6 *@6 *@6 *@4 *@2 *@2"
			readvar $line "$id@5 *@1 $name@4 *@14 $rx@8 $ry@8 $rz@8 *@22 $e@2"
			if $e = ""
				$e = $name
			end
			newatom $e $rx $ry $rz
		elseif $keyword = "ATOM"
			readvar $line "$id@5 *@1 $name@4 *@14 $rx@8 $ry@8 $rz@8 *@22 $e@2"
			if $e = ""
				$e = $name
			end
			newatom $e
			setcoords $rx $ry $rz
		elseif $keyword = "CONECT"
			$bondi = 0
			$bondj = 0
			$bondk = 0
			$bondl = 0
			readvar $line "$i $bondi $bondj $bondk $bondl"
			if $bondi > 0
				newbond $i $bondi
			end
			if $bondj > 0
				newbond $i $bondj
			end
			if $bondk > 0
				newbond $i $bondk
			end
			if $bondl > 0
				newbond $i $bondl
			end
		end
	end
	if $found = 0
		warn "No HEADER record found in file."
	end
	rebond
	finalisemodel
end
