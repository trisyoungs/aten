# Simple-style volumetric data / probability density (for v1.2+)
# Created:	 15/04/2009
# Last modified: 03/05/2010
# ChangeLog:
#	03/05/2010 - Adjusted to use modified grid commands
# Notes:

filter(type="importgrid", name="Probability Density", nickname="pdens", extension="pdens", glob="*.pdens", id=2)
{
	# Variable declarations
	string order;
	int n,nx,ny,nz,npoints;
	vector origin, a, b, c;
	double data;

	# Create new grid
	newgrid(filterfilename());

	# First line contains number of gridpoints in each direction x,y,z
	readline(nx, ny, nz);
	initgrid("regularxyz",nx,ny,nz);

	# Second line contains axis system
	readline(a.x, a.y, a.z, b.x, b.y, b.z, c.x, c.y, c.z);
	gridaxes(a.x, a.y, a.z, b.x, b.y, b.z, c.x, c.y, c.z);

	# Third line contains grid origin (lower left-hand corner)
	readline(origin.x, origin.y, origin.z);
	gridorigin(origin.x, origin.y, origin.z);

	# Fourth line is loop order
	readline(order);
	gridlooporder(order);

	# Read in grid data
	npoints = nx*ny*nz;
	for (n=1; n<=npoints; ++n)
	{
		readline(data);
		addnextgridpoint(data);
	}
	finalisegrid();
}
