# DL_POLY Unformatted Trajectory file filter.
# Last modified: 25/10/07.
importtrajectory
	name "DL_POLY Unformatted History"
	extension "HISu"
	glob *HISu
	nickname dlpolyu
	exact "HISTORY"
	# DL_POLY unformatted history files may contain a header.
	# Since its a Fortran code, each record in the file is surrounded by a single integer specifying
	# the record size in characters.
	# When aten is trying to read the header, the variable 'header' will be 'true' (and 'frame' will be 'false')
	if $header = true
		# First line is 80-character header.
		readint $recsize
		readchars $title 80
		readint $recsize
		# Next, number of atoms in the system (as a double)
		readint $recsize
		readdouble $dnatoms
		let $natoms = $dnatoms
		readint $recsize
		# Atom names
		readint $recsize
		repeat $n $natoms
			readchars $name 8
		end
		readint $recsize
		# Atomic masses
		readint $recsize
		repeat $n $natoms
			readdouble $mass
		end
		readint $recsize
		# Atom charges
		readint $recsize
		repeat $n $natoms
			readdouble $q
		end
		readint $recsize
	else
		# First data for frame is : nstep, $natoms, keytrj, imcon, tstep, all as doubles 
		readint $recsize
		readdouble $dnstep
		readdouble $dnatoms
		readdouble $dkeytrj
		readdouble $dimcon
		readdouble $tstep
		readint $recsize
		# Unit cell
		if $cell.type <> none
			readint $recsize
			readdouble $cell.a.x
			readdouble $cell.a.y
			readdouble $cell.a.z
			readdouble $cell.b.x
			readdouble $cell.b.y
			readdouble $cell.b.z
			readdouble $cell.c.x
			readdouble $cell.c.y
			readdouble $cell.c.z
			readint $recsize
			setcellaxes
		end
		# Create atoms in model all at once, since we need to reference them all at different points
		modeltemplate
		# X-Coordinates
		readint $recsize
		repeat $n $natoms
			readdouble $r.x
			setrx $n $r.x
		end
		readint $recsize
		# Y-Coordinates
		readint $recsize
		repeat $n $natoms
			readdouble $r.y
			setry $n $r.y
		end
		readint $recsize
		# Z-Coordinates
		readint $recsize
		repeat $n $natoms
			readdouble $r.z
			setrz $n $r.z
		end
		readint $recsize
		# X-Velocities
		readint $recsize
		repeat $n $natoms
			readdouble $v.x
			setvx $n $v.x
		end
		readint $recsize
		# Y-Velocities
		readint $recsize
		repeat $n $natoms
			readdouble $v.y
			setvy $n $v.y
		end
		readint $recsize
		# Z-Velocities
		readint $recsize
		repeat $n $natoms
			readdouble $v.z
			setvz $n $v.z
		end
		readint $recsize
		# X-Forces
		readint $recsize
		repeat $n $natoms
			readdouble $f.x
			setfx $n $f.x
		end
		readint $recsize
		# Y-Forces
		readint $recsize
		repeat $n $natoms
			readdouble $f.y
			setfy $n $f.y
		end
		readint $recsize
		# Z-Forces
		readint $recsize
		repeat $n $natoms
			readdouble $f.z
			setfz $n $f.z
		end
		readint $recsize
		fold
		rebond
	end
end
