# Gaussian Cube Format
# Last modified: 12/10/2008
# ChangeLog:
#	12/10/2008 - Adjusted to use new variable access rules.
#	28/10/2007 - First version.

importgrid
	name "Gaussian CUBE"
	nickname cube
	extension cube
	glob *.cube
	zmap numeric

	# Variable declaration
	character e,title
	integer natoms,nx,ny,nz,i,j,k
	double ox,oy,oz,xx,xy,xz,yx,yy,yz,zx,zy,zz,rx,ry,rz,data

	# Since we must create the model first, and then set all grid date afterwards,
	# store all grid-related values for later until we've finalised the model.

	# Lines 1-2: Comments (use first as title)
	getline $title
	skipline

	# Line 3: Number of atoms and coordinate origin of surface
	readline "$natoms $ox $oy $oz"

	# Lines 4-6: Number of voxels and axis vector
	readline "$nx $xx $xy $xz"
	readline "$ny $yx $yy $yz"
	readline "$nz $zx $zy $zz"

	# Lines 7-(7+natoms) Molecule Definition
	newmodel $title
	for $i,1,$natoms
		readline "$e * $rx $ry $rz"
		newatom $e $rx $ry $rz
	end
	finalisemodel
	rebond

	# Create a grid associated to this model, and set it up with data we read previously
	newgrid $title
	gridorigin $ox $oy $oz
	gridsize $nx $ny $nz
	gridaxes $xx $xy $xz $yx $yy $yz $zx $zy $zz

	# Volumetric data (five points per line)
	for $i,1,$nx
		for $j,1,$ny
			for $k,1,$nz
				readnext $data
				addgridpoint $i $j $k $data
			end
		end
	end
	finalisegrid
end
