#
# Script to create a disordered [dmim]Cl system
#

# Load Canongia Lopes ionic liquids forcefield (giving it a nickname of 'ilff')
# This will become the current forcefield (used by the 'ff' commands later on)
loadff data/ff/cldp-il.ff ilff

# Prepare first component to add in - [dmim] cation
# Load model from file and call it 'dmim'
loadmodel test/siesta-dmim.xyz dmim

# Associate the current forcefield (the only one) to the model
ffmodel

# Create a forcefield expression for the model (determine atom types, bond types, etc.)
createexpression

# Assign atomic partial charges from the forcefield as well
chargeff

# Print out the model data 
info

# Prepare second component - chloride anion
# Create a new model (naming it 'chloride') and draw a chloride atom
newmodel chloride
newatom Cl

# Associate forcefield to the model
ffmodel

# Create the forcefield expression
createexpression

# Assign charges from the forcefield
chargeff

# Print the data
info

# New we're ready to use these two components to create a new system.
# First, create another empty model (called 'box') and create a cubic
# cell in it
newmodel box
setcell 30.0 30.0 30.0 90.0 90.0 90.0

# Associate the forcefield with the box (there are no atoms in it, but it still needs a valid forcefield)
ffmodel


# Request that we want 100 molecules maximum of each (how many you end up with will vary)
addcomponent cation dmim 100
addcomponent anion chloride 100

# Tweak the Monte Carlo parameters for the disordered insertion
mcntrials insert 50
mcaccept insert 100.0
printmc

# Request the Ewald sum for electrostatics (or none for just VDW intermolecular energies)
#elec ewald 0.256 10 10 10
elec off
printsetup

# Print out the current component list for the disordered builder
listcomponents

# To squeeze more molecule in, we can adjust the VDW radii of all atoms with this scale factor
vdwscale 0.75

# Begin! Run the Monte Carlo for 10 cycles
disorder 10

# Save out the current model (will be 'box', since it was the last created) as a DL_POLY config file
savemodel dlpoly test.CONFIG

# Done.
#quit
