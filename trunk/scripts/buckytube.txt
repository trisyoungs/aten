# Create a buckytube of some length and 'radius' along the z-axis

# Variables
integer nbenz, nstacks, stack, benz
double rcc, deltaz

# Setup
# The number of benzene laying on the same stack circumference is nbenz
$nbenz = 20 
# C-C bond length is given by rcc
$rcc = 1.39
# Number of stacks of molecules to generate (length of tube)
$nstacks = 50
# Rotation increment around circumference is deltaz
$deltaz = 360.0/($nbenz*2.0)

# Create target model
newmodel buckytube

# Outer loop - loop over stacks
for $stack,1,$nstacks

	# Inner loop - loop over benzene molecules
	for $benz,1,$nbenz
		newatom C
		move $rcc*cos(30.0) 0.0 $rcc*sin(30.0)
		rotz $deltaz
		newatom C
		move $rcc*cos(30.0) 0.0 -$rcc*sin(30.0)
		rotz $deltaz
	end
	
	# Move pen along tube length, and one step around circumference to offset next layer
	move 0.0 0.0 $rcc*1.5
	move $rcc*cos(30.0) 0 0
	rotz $deltaz

end
